plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace = "dev.myallworks.native_orm_db"
    compileSdk = 34

    defaultConfig {
        minSdk = 24
        targetSdk = 34

        consumerProguardFiles "consumer-rules.pro"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.11.0"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
}

// Define your library version
ext.PUBLISH_GROUP_ID = 'io.github.dnomahesh'
ext.PUBLISH_ARTIFACT_ID = 'native-orm-db'
ext.PUBLISH_VERSION = '1.0.0'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                pom {
                    name = 'Native ORM DB'
                    description = 'A lightweight ORM alternative to LitePal for Android development'
                    url = 'https://github.com/dnomahesh/native_orm_db'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'dnomahesh'
                            name = 'Mahesh Tripathi'
                            email = 'maheshtripathijobs247@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/dnomahesh/native_orm_db.git'
                        developerConnection = 'scm:git:ssh://github.com:dnomahesh/native_orm_db.git'
                        url = 'https://github.com/dnomahesh/native_orm_db/tree/main'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "sonatype"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
                findProperty("signingKeyId") ?: System.getenv("SIGNING_KEY_ID"),
                findProperty("signingKey") ?: System.getenv("SIGNING_KEY"),
                findProperty("signingPassword") ?: System.getenv("SIGNING_PASSWORD")
        )
        sign publishing.publications["release"]
    }
}